
FUNCTION_BLOCK attack

VAR_INPUT
    angle: REAL;
    distance : REAL;
    direction : REAL;
END_VAR

VAR_OUTPUT
    activate : REAL;
END_VAR

FUZZIFY angle
    TERM front := (0, 1) (1.555, 0) ; 
    TERM back := (0.7854, 0) (3.142, 1);
END_FUZZIFY

FUZZIFY distance
    TERM close := (0, 1) (0.5, 0);
    TERM far := (0.25, 0) (1, 1);
END_FUZZIFY

FUZZIFY direction
    TERM same := (0, 1) (3.142, 0) ; 
    TERM inverse := (0, 0) (3.142, 1);
END_FUZZIFY

DEFUZZIFY activate
    TERM no := (-1, 1) (0, 0);
    TERM maybe := (-0.5, 0) (0, 1) (0.5, 0);
    TERM yes := (0, 0) (1, 1);

    METHOD : COG;
    DEFAULT := -1;
END_DEFUZZIFY

RULEBLOCK No1
    AND : MIN;
    ACT : MIN;
    ACCU : MAX;

    RULE 1 : IF angle IS front AND distance IS close AND direction IS same
                THEN activate IS no;

    RULE 2 : IF angle IS front AND distance IS close AND direction IS inverse
                THEN activate IS no;

    RULE 3 : IF angle IS front AND distance IS far AND direction IS same
                THEN activate IS no;

    RULE 4 : IF angle IS front AND distance IS far AND direction IS inverse
                THEN activate IS no;

    RULE 5 : IF angle IS back AND distance IS close AND direction IS same
                THEN activate IS yes;

    RULE 6 : IF angle IS back AND distance IS close AND direction IS inverse
                THEN activate IS no;

    RULE 7 : IF angle IS back AND distance IS far AND direction IS same
                THEN activate IS maybe;

    RULE 8 : IF angle IS back AND distance IS far AND direction IS inverse
                THEN activate IS no;

END_RULEBLOCK

END_FUNCTION_BLOCK